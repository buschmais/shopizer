[[preparation:Default]]
[role=group,includesConcepts="preparation:ShopizerFiles,preparation:ShopizerLayers,preparation:ShopizerFilesToLayers,preparation:ShopizerFilesToLayersPackage"]
== Preparation

These concepts define basic labels and further enrichments for the dataset.

=== Concepts

[[preparation:ShopizerFiles]]
[source,cypher,role="concept"]
.Labels all Shopizer artifacts and contained files with "Shopizer".
----
MATCH
    (a:Artifact)
WHERE
    a.fqn IN [
        "com.shopizer:shopizer:pom:2.2.0-SNAPSHOT",
        "com.shopizer:sm-core:jar:2.2.0-SNAPSHOT",
        "com.shopizer:sm-core-model:jar:2.2.0-SNAPSHOT",
        "com.shopizer:sm-core-model-api:jar:2.2.0-SNAPSHOT",
        "com.shopizer:sm-core-modules:jar:2.2.0-SNAPSHOT",
        "com.shopizer:sm-shop:war:2.2.0-SNAPSHOT",
        "com.shopizer:sm-common:jar:2.2.0-SNAPSHOT",
        "com.shopizer:sm-catalog:jar:2.2.0-SNAPSHOT"
    ]
SET
    a:Shopizer
WITH
    a
MATCH
    (a)-[:CONTAINS*]->(c)
SET
    c:Shopizer
RETURN
    a.name AS Artifact, count(DISTINCT c) AS Content_Cnt
ORDER BY
    a.name
----

[[preparation:ShopizerLayers]]
[source,cypher,role="concept"]
.Creates three layer nodes with individual names.
----
CREATE
  (dataLayer:Layer {name:"DataLayer"}),
  (domainLayer:Layer {name:"DomainLayer"}),
  (presentationLayer:Layer {name:"PresentationLayer"})
RETURN
  dataLayer, domainLayer, presentationLayer
----

[[preparation:ShopizerFilesToLayers]]
[source,cypher,role="concept",requiresConcepts="preparation:ShopizerLayers,preparation:ShopizerFiles"]
.Assigns all files to a technical layer based on the containing artifact.
----
MATCH
    (a:Artifact:Shopizer)-[:CONTAINS*]->(t)
WITH DISTINCT t,
    CASE a.name
        WHEN "sm-shop" THEN "PresentationLayer"
        WHEN "sm-core" THEN "DomainLayer"
        WHEN "sm-core-modules" THEN "DomainLayer"
        WHEN "sm-core-model" THEN "DataLayer"
        WHEN "sm-core-model-api" THEN "DataLayer"
    END AS layer
MATCH
    (l:Layer {name:layer})
MERGE
    (l)-[:CONTAINS]->(t)
RETURN
    l.name AS Layer, count(DISTINCT t) AS Size
----

[[preparation:ShopizerFilesToLayersPackage]]
[source,cypher,role="concept",requiresConcepts="preparation:ShopizerLayers,preparation:ShopizerFiles"]
.Assigns all files to a technical layer based on the containing package.
----
MATCH
    (p:Package)-[:CONTAINS*]->(t)
WITH DISTINCT t,
    CASE p.fqn
        WHEN "com.salesmanager.common.presentation" THEN "PresentationLayer"
        WHEN "com.salesmanager.common.business" THEN "DomainLayer"
        WHEN "com.salesmanager.common.model" THEN "DataLayer"
    END AS layer
MATCH
    (l:Layer {name:layer})
MERGE
    (l)-[:CONTAINS]->(t)
RETURN
    l.name AS Layer, count(DISTINCT t) AS Size
----