[[bounded-context:Default]]
[role=group,includesConcepts="bounded-context:*",includesConstraints="bounded-context:*"]
=== Bounded Context

The application consists of `Bounded Contexts`, each of them consisting of one or more Maven artifacts.

[[bounded-context:BoundedContext]]
[source,cypher,role=concept]
.Defined Bounded Contexts and their mapping to Maven artifacts.
----
UNWIND
  [
    {id:"Core", artifacts:["sm-core","sm-core-model","sm-core-modules","sm-shop"]},
    {id:"Catalog", artifacts:["sm-catalog"]}
  ] as definition
UNWIND
  definition.artifacts as artifactId
MATCH
  (bc:Main:Artifact{name:artifactId})
SET
  bc:BoundedContext
SET
  bc.boundedContext = definition.id
RETURN
  bc.boundedContext as BoundedContext, collect(bc.name) as Artifacts
----

Bounded Contexts are completely independent of each other, i.e. they do not share common Java types.

[[bounded-context:UndefinedDependency]]
[source,cypher,role=constraint]
.A bounded context must not have dependencies to other bounded contexts.
----
MATCH
  (bc1:BoundedContext:Artifact)-[:CONTAINS]->(t1:Type),
  (bc2:BoundedContext:Artifact)-[:CONTAINS]->(t2:Type),
  (t1)-[dependsOn:DEPENDS_ON]->(t2)
WHERE
  bc1.boundedContext <> bc2.boundedContext
RETURN
  bc1.boundedContext as BoundedContext, t1 as Type, bc2.boundedContext as Dependency, t2 as TypeDependency
----

